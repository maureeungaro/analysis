#!/bin/csh -f

# If the option TEST is used, MULTI_JOBS is set to 5
# Note: with SLURM the log files are inside /farm_out/ungaro/

# Parameters set by user
# ----------------------

set GCARDSD    = ~/projects/analysis/farm/gemc/studies/vextension  # collection of gcards. One job / gcard will be launched
set NEVENTS    = 20                              # number of events / job
set MULTI_JOBS = 100                             # number of jobs to be launched per gcard
set OUTPUTD    = /volatile/clas12/ungaro/clas12  # output directory
set DEBUG      = no                              # "yes" will print out the jsub file instead of submitting it
set EXPSD      = /group/clas12/gemc/4.4.0        # directory containing the detector definition
set INPUTFILES = none                            # directory containing the input files. Can be "none" for internal generator
                                                 # The files must be of the format "gemcInput_#.dat" where # (file number) will match the job number

if($1 == "TEST") then
	set MULTI_JOBS = 5
endif

# Farm specific parameters
set MEMORY   = "4000 MB"       # node memory to be used
set OS       = centos7         # job OS
set PROJECT  = clas12          # job project name
set MAIL     = ungaro@jlab.org # author email
set TRACK    = simulation      # job track
set JSUBC    = /site/bin/jsub                       # command to submit the job
set JSUBC    = /site/scicomp/auger-slurm/bin/jsub   # SLURM command to submit the job
set TIME     = 340             # max time in minutes 4 hours

# End of user parameter
# ---------------------


# title of the jobs to be launched
set titles    = `./functions/getGcardsList $GCARDSD`
echo
echo " > Gcards to be launched from " $GCARDSD " :"
echo
echo " >" $titles
echo

set scriptsd  = `pwd`
set i         = 1

# Settings defined here:
# https://scicomp.jlab.org/docs/text_command_file
# Time is in minutes
# Memory needs units


set w = 1
while ($w <= $#titles)
	set gcard = $titles[$w]".gcard"
	rm -f gemc.jsub
	\echo  JOBNAME: $titles[$w]                                                    > gemc.jsub
	\echo  PROJECT: $PROJECT                                                      >> gemc.jsub
	\echo  MAIL:    $MAIL                                                         >> gemc.jsub
	\echo  TRACK:   $TRACK                                                        >> gemc.jsub
	\echo  SINGLE_JOB: true                                                       >> gemc.jsub
	\echo  OS:      $OS                                                           >> gemc.jsub
	\echo  MEMORY:  $MEMORY                                                       >> gemc.jsub
	\echo  TIME:    $TIME                                                         >> gemc.jsub
	\echo  MULTI_JOBS: $MULTI_JOBS                                                >> gemc.jsub
	\echo  COMMAND: ./goSimulate $titles[$w] $OUTPUTD $NEVENTS $EXPSD $INPUTFILES >> gemc.jsub
	\echo  OTHER_FILES: $scriptsd/goSimulate                                      >> gemc.jsub
	\echo  OTHER_FILES: $scriptsd/functions/logConditions                         >> gemc.jsub
	\echo  OTHER_FILES: $GCARDSD/$gcard                                           >> gemc.jsub
	\echo
	\echo Launching $MULTI_JOBS jobs for for $titles[$w]
	\echo
	if($DEBUG != "yes") then
		$JSUBC gemc.jsub
	else
		cat  gemc.jsub
	endif
	@ w += 1
end
